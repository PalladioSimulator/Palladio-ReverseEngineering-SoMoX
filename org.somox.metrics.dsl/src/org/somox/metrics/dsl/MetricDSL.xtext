grammar org.somox.metrics.dsl.MetricDSL with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate metricDSL "http://www.somox.org/metricDSL/1.0"

MetricModel :
        ('import' importURI += STRING ';' )*
        ('extern' metrics += ExternalMetric ';' )*
        'Metrics' '{'
                metrics += InternalMetric+
        '}';

Metric:
        ExternalMetric | InternalMetric;
        
ExternalMetric :
        name = MYID;
                
InternalMetric :
        'Metric' name = MYID '(' shortName = STRING ',' description = STRING ')' '{'
                ('parameters' '{'
                        parameter += Number*
                '}')? 
                definition = MetricDefinition
        '};'; 

Number:
        Parameter | Constant;
        
Parameter:
    'Parameter' name = MYID '(' shortname = STRING ',' description = STRING ',' defaultValue = DOUBLE ')' ';';

Constant:
        'Const' name = MYID '=' value = DOUBLE ';';   
                
MetricDefinition :
        WeightedMetric | StepwiseMetric | RatioMetric ;
        
WeightedMetric :
        'WeigthedSum' '{'
                weights += MetricAndWeight+
        '}';

StepwiseMetric :
        'Stepwise' '[' innerMetric = [Metric|MYID] ']' '{'
                steps += BoundAndWeight*
        '}';

RatioMetric :
        'Ratio' '{'
                nominatorMetric = [Metric|MYID] '/' denominatorMetric = [Metric|MYID]
        '}';
        
BoundAndWeight :
        '<' upperBound = [Number|MYID] ',' weight = [Number|MYID] '>';

MetricAndWeight :
        '<' metric = [Metric|MYID] ',' weight = [Number|MYID] '>';

MYID returns ecore::EString : ID ( '.' ID )*;
                  
terminal DOUBLE returns ecore::EDouble : ('-')? INT '.' INT;